{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Variable coefficient $p$-system with periodic BCs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"setup.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Compute the energy for one chosen setting"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# setup similar to https://github.com/clawpack/pyclaw/blob/master/examples/stegoton_1d/stegoton.py\n",
    "# but with smooth ϱ, K\n",
    "function ϱ_exp(x)\n",
    "    2.5 - 1.5 * sinpi(2x)\n",
    "end\n",
    "K_exp(x) = ϱ_exp(x)\n",
    "function σ_exp(ε::SymPy.Sym, x)\n",
    "    exp(K_exp(x) * ε) - 1\n",
    "end\n",
    "function σ_exp(ε, x)\n",
    "    expm1(K_exp(x) * ε)\n",
    "end\n",
    "function η_exp(ε, u, x)\n",
    "    K = K_exp(x)\n",
    "    exp(ε * K) / K - ε - inv(K) + ϱ_exp(x) * u^2 / 2\n",
    "end\n",
    "\n",
    "function doit(σ)\n",
    "    ε, x, u = sp.symbols(\"varepsilon, x, u\", real=true)\n",
    "    \n",
    "    antiderivative = sp.integrate(σ(ε, x), ε)\n",
    "    antiderivative = antiderivative - antiderivative.subs(ε, 0)\n",
    "    antiderivative + ϱ_exp(x) * u^2 / 2 - η_exp(ε, u, x) |> sp.simplify\n",
    "end\n",
    "\n",
    "doit(σ_exp)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Discretization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function relaxation_functional(γ, εu_new, εu_old, param)\n",
    "    @unpack D, x, tmp1, η = param\n",
    "    ε_new = εu_new.x[1]\n",
    "    u_new = εu_new.x[2]\n",
    "    ε_old = εu_old.x[1]\n",
    "    u_old = εu_old.x[2]\n",
    "    \n",
    "    @. tmp1 = η((1-γ)*ε_old + γ*ε_new, (1-γ)*u_old + γ*u_new, x)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "function relaxation_functional(εu, param)\n",
    "    @unpack D, x, tmp1, η = param\n",
    "    ε = εu.x[1]\n",
    "    u = εu.x[2]\n",
    "    \n",
    "    @. tmp1 = η(ε, u, x)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_periodic(εu, t, integrator)\n",
    "    @unpack D, x, tmp1, ϱ, η, σ, ε_sol, u_sol = integrator.p\n",
    "    ε = εu.x[1]\n",
    "    u = εu.x[2]\n",
    "    print(\".\")\n",
    "    \n",
    "    mass_ε = integrate(ε, D)\n",
    "    @. tmp1 = ϱ(x) * u\n",
    "    mass_u = integrate(tmp1, D)\n",
    "    energy = relaxation_functional(εu, integrator.p)\n",
    "    \n",
    "    @. tmp1 = (ε - ε_sol(t, x))^2\n",
    "    error_ε = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    @. tmp1 = (u - u_sol(t, x))^2\n",
    "    error_u = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    @. tmp1 = (σ(ε, x) - σ(ε_sol(t, x), x))^2\n",
    "    error_σ = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    @. tmp1 = (ϱ(x)*u - ϱ(x)*u_sol(t, x))^2\n",
    "    error_ϱu = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    SVector(mass_ε, mass_u, energy, error_ε, error_u, error_σ, error_ϱu)\n",
    "end\n",
    "\n",
    "\n",
    "function psystem_periodic!(dεu, εu, param, t)\n",
    "    @unpack D, x, tmp1, σ, ϱ = param\n",
    "    ε  = εu.x[1]\n",
    "    u  = εu.x[2]\n",
    "    dε = dεu.x[1]\n",
    "    du = dεu.x[2]\n",
    "    \n",
    "    mul!(dε, D, u)\n",
    "    @. tmp1 = σ(ε, x)\n",
    "    mul!(du, D, tmp1)\n",
    "    @. du = du / ϱ(x)\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "function solve_ode_psystem_periodic(ε_sol, u_sol, D, σ, ϱ, η, tspan, alg, tol, dt, adaptive, num_periods)\n",
    "    x = grid(D)\n",
    "    ε0 = ε_sol.(tspan[1], x)\n",
    "    u0 = u_sol.(tspan[1], x)\n",
    "    εu0 = ArrayPartition(ε0, u0)\n",
    "    tmp1 = similar(u0)\n",
    "    param = (;D, x, tmp1, σ, ϱ, η, u_sol, ε_sol)\n",
    "\n",
    "    ode = ODEProblem(psystem_periodic!, εu0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=num_periods+1)\n",
    "    saved_values_baseline = SavedValues(eltype(D), SVector{7,eltype(D)})\n",
    "    saving_baseline = SavingCallback(save_func_periodic, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D), SVector{7,eltype(D)})\n",
    "    saving_relaxation = SavingCallback(save_func_periodic, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!,\n",
    "        save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    \n",
    "    sol_baseline = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, \n",
    "        callback=cb_baseline, dense=false,\n",
    "        save_everystep=false, tstops=saveat, saveat=saveat)\n",
    "    flush(stdout)\n",
    "    sol_relaxation = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive,\n",
    "        callback=cb_relaxation, dense=false,\n",
    "        save_everystep=false, tstops=saveat, saveat=saveat)\n",
    "    flush(stdout)\n",
    "\n",
    "    ε_num_baseline   = sol_baseline[end].x[1]\n",
    "    u_num_baseline   = sol_baseline[end].x[2]\n",
    "    ε_num_relaxation = sol_relaxation[end].x[1]\n",
    "    u_num_relaxation = sol_relaxation[end].x[2]\n",
    "    ε_ana = ε_sol.(tspan[end], x)\n",
    "    u_ana = u_sol.(tspan[end], x)\n",
    "    @printf(\"Error in ε (baseline):   %.3e\\n\", integrate(u->u^2, ε_num_baseline   - ε_ana, D) |> sqrt)\n",
    "    @printf(\"Error in ε (relaxation): %.3e\\n\", integrate(u->u^2, ε_num_relaxation - ε_ana, D) |> sqrt)\n",
    "    @printf(\"Error in u (baseline):   %.3e\\n\", integrate(u->u^2, u_num_baseline   - u_ana, D) |> sqrt)\n",
    "    @printf(\"Error in u (relaxation): %.3e\\n\", integrate(u->u^2, u_num_relaxation - u_ana, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in ε: %.3e\\n\", \n",
    "        integrate(u->u^2, ε_num_baseline - ε_num_relaxation, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in u: %.3e\\n\", \n",
    "        integrate(u->u^2, u_num_baseline - u_num_relaxation, D) |> sqrt)\n",
    "\n",
    "    sleep(0.1)\n",
    "    fig_ε, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, ε0, label=L\"\\varepsilon^0\")\n",
    "    plt.plot(x, ε_num_baseline,   label=L\"$\\varepsilon^\\mathrm{num}$ (non-conservative)\")\n",
    "    plt.plot(x, ε_num_relaxation, label=L\"$\\varepsilon^\\mathrm{num}$ (conservative)\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"\\varepsilon\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    fig_σ, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, σ.(ε0, x), label=L\"\\sigma^0\")\n",
    "    plt.plot(x, σ.(ε_num_baseline, x),   label=L\"$\\sigma^\\mathrm{num}$ (non-conservative)\")\n",
    "    plt.plot(x, σ.(ε_num_relaxation, x), label=L\"$\\sigma^\\mathrm{num}$ (conservative)\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"\\sigma\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    fig_u, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, u0, label=L\"u^0\")\n",
    "#     plt.plot(x, u_ana, label=L\"$u^\\mathrm{ana}$\")\n",
    "    plt.plot(x, u_num_baseline,   label=L\"$u^\\mathrm{num}$ (non-conservative)\")\n",
    "    plt.plot(x, u_num_relaxation, label=L\"$u^\\mathrm{num}$ (conservative)\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    mass_ε_baseline   = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_ε_relaxation = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    mass_u_baseline   = map(x->x[2], saved_values_baseline.saveval)\n",
    "    mass_u_relaxation = map(x->x[2], saved_values_relaxation.saveval)\n",
    "    energy_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    energy_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_ε_baseline   .- mass_ε_baseline[1],   \n",
    "        label=L\"$\\int \\varepsilon$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_ε_relaxation .- mass_ε_relaxation[1], \n",
    "        label=L\"$\\int \\varepsilon$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   mass_u_baseline   .- mass_u_baseline[1],   \n",
    "        label=L\"$\\int u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, mass_u_relaxation .- mass_u_relaxation[1],\n",
    "        label=L\"$\\int u$ (conservative)\", color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.plot(t_baseline,   energy_baseline   .- energy_baseline[1],   \n",
    "        label=\"Energy (non-conservative)\", color=\"#E69F00\", linestyle=\":\")\n",
    "    plt.plot(t_relaxation, energy_relaxation .- energy_relaxation[1], \n",
    "        label=\"Energy (conservative)\", color=\"#56B4E9\", linestyle=\":\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_ε_baseline   = map(x->x[4], saved_values_baseline.saveval)\n",
    "    error_ε_relaxation = map(x->x[4], saved_values_relaxation.saveval)\n",
    "    error_u_baseline   = map(x->x[5], saved_values_baseline.saveval)\n",
    "    error_u_relaxation = map(x->x[5], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error1, ax = plt.subplots(1, 1)\n",
    "    if num_periods < 50\n",
    "        ax.set_prop_cycle(marker_cycler)\n",
    "    end\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_ε_baseline,   label=L\"Error of $\\varepsilon$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, error_ε_relaxation, label=L\"Error of $\\varepsilon$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   error_u_baseline,   label=L\"Error of $u$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, error_u_relaxation, label=L\"Error of $u$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_σ_baseline    = map(x->x[6], saved_values_baseline.saveval)\n",
    "    error_σ_relaxation  = map(x->x[6], saved_values_relaxation.saveval)\n",
    "    error_ϱu_baseline   = map(x->x[7], saved_values_baseline.saveval)\n",
    "    error_ϱu_relaxation = map(x->x[7], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error2, ax = plt.subplots(1, 1)\n",
    "    if num_periods < 50\n",
    "        ax.set_prop_cycle(marker_cycler)\n",
    "    end\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_σ_baseline,    label=L\"Error of $\\sigma$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, error_σ_relaxation,  label=L\"Error of $\\sigma$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   error_ϱu_baseline,   label=L\"Error of $\\varrho u$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, error_ϱu_relaxation, label=L\"Error of $\\varrho u$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "\n",
    "    fig_coef, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, ϱ.(x), label=L\"\\varrho\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(\"Variable coefficients\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "        \n",
    "    (;sol_baseline, sol_relaxation, saved_values_baseline, saved_values_relaxation, \n",
    "        fig_ε, fig_σ, fig_u, fig_invariants, fig_error1, fig_error2, fig_coef)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Error growth of numerically generated solitary waves"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# using a stegoton generated numerically\n",
    "function mean_to_center(q)\n",
    "    nghost = 2\n",
    "    qBCs = similar(q, length(q) + 2*nghost)\n",
    "    qBCs[1:nghost] = q[end:-1:end-nghost+1]\n",
    "    qBCs[nghost+1:end-nghost] = q\n",
    "    qBCs[end-nghost+1:end] = q[1:nghost]\n",
    "    res = @views @. (9*qBCs[1:end-4] - 116*qBCs[2:end-3] + 2134*qBCs[3:end-2] - 116*qBCs[4:end-1] + 9*qBCs[5:end]) / 1920.0\n",
    "end\n",
    "\n",
    "data_filename = joinpath(dirname(@__DIR__), \"data\", \"psystem_refn3_small_domain.h5\")\n",
    "x_center, ε_center, ϱu_center = h5open(data_filename, \"r\") do io\n",
    "    read(io, \"x\"), read(io, \"strain\"), read(io, \"vel\")\n",
    "end\n",
    "\n",
    "u_center = @. ϱu_center / ϱ_exp(x_center)\n",
    "dx = (x_center[end] - x_center[1]) / (length(x_center) - 1)\n",
    "xmin = x_center[1]; xmax = x_center[end]\n",
    "x = range(xmin, xmax, length=length(x_center))\n",
    "\n",
    "ε0itp = CubicSplineInterpolation((x,), mean_to_center(ε_center), extrapolation_bc=Periodic())\n",
    "u0itp = CubicSplineInterpolation((x,), mean_to_center(u_center), extrapolation_bc=Periodic())\n",
    "\n",
    "get_c() = 0.9769660271402583 # fine grid estimate\n",
    "function ε_sol(t, x)\n",
    "    xmin = 0.0\n",
    "    xmax = 20.0\n",
    "    c = get_c()\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    ε0itp(x_t)::Float64\n",
    "end\n",
    "function u_sol(t, x)\n",
    "    xmin = 0.0\n",
    "    xmax = 20.0\n",
    "    c = get_c()\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    u0itp(x_t)::Float64\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@show xmin\n",
    "@show xmax\n",
    "@show ε_sol(0., xmin)\n",
    "@show ε_sol(0., xmax)\n",
    "@show u_sol(0., xmin)\n",
    "@show u_sol(0., xmax)\n",
    "@show N = 2^8\n",
    "@show dt = 1.0e-1 * (xmax - xmin) / N\n",
    "\n",
    "@show num_periods = 350\n",
    "@show tspan = (0., num_periods * (xmax - xmin) / get_c())\n",
    "flush(stdout)\n",
    "\n",
    "tol = 1.0e-6\n",
    "adaptive = true\n",
    "D = fourier_derivative_operator(xmin, xmax, N)\n",
    "\n",
    "results = solve_ode_psystem_periodic(ε_sol, u_sol, D, σ_exp, ϱ_exp, η_exp, tspan, \n",
    "                                     Tsit5(), tol, dt, adaptive, num_periods);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ax = results.fig_error1.axes[1]\n",
    "t = [1.0e3, 8.0e3]\n",
    "ax.plot(t, 1.5e-8 .* t.^2, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{2})\", (1.0e3, 1.5e-1), color=\"gray\")\n",
    "ax.plot(t, 3.0e-6 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e3, 2.0e-3), color=\"gray\")\n",
    "results.fig_error1.savefig(\n",
    "    joinpath(dirname(@__DIR__), \"figures\", \"p_system_Fourier_error_epsilon_u.pdf\"),\n",
    "    bbox_inches=\"tight\")\n",
    "results.fig_error2.savefig(\n",
    "    joinpath(dirname(@__DIR__), \"figures\", \"p_system_Fourier_error_sigma_rhou.pdf\"),\n",
    "    bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ε0 = results.sol_baseline[1].x[1]\n",
    "u0 = results.sol_baseline[1].x[2]\n",
    "ε_num_baseline   = results.sol_baseline[end].x[1]\n",
    "u_num_baseline   = results.sol_baseline[end].x[2]\n",
    "ε_num_relaxation = results.sol_relaxation[end].x[1]\n",
    "u_num_relaxation = results.sol_relaxation[end].x[2]\n",
    "x = grid(results.sol_baseline.prob.p.D)\n",
    "ε_ana = ε_sol.(tspan[end], x)\n",
    "u_ana = u_sol.(tspan[end], x)\n",
    "\n",
    "fig_ε, ax = plt.subplots(1, 1)\n",
    "plt.plot(x, ε_num_baseline,   label=\"non-conservative\")\n",
    "plt.plot(x, ε_num_relaxation, label=\"conservative\")\n",
    "plt.plot(x, ε0, \":\", color=\"gray\", label=\"reference\")\n",
    "plt.xlabel(L\"x\"); plt.ylabel(L\"\\varepsilon\")\n",
    "plt.xlim(xmin, xmax)\n",
    "plt.savefig(joinpath(dirname(@__DIR__), \"figures\", \"p_system_Fourier_solution_epsilon.pdf\"), bbox_inches=\"tight\")\n",
    "\n",
    "fig_u, ax = plt.subplots(1, 1)\n",
    "plt.plot(x, u_num_baseline)\n",
    "plt.plot(x, u_num_relaxation)\n",
    "plt.plot(x, u0, \":\", color=\"gray\", label=\"erference\")\n",
    "plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "plt.xlim(xmin, xmax)\n",
    "plt.savefig(joinpath(dirname(@__DIR__), \"figures\", \"p_system_Fourier_solution_u.pdf\"), bbox_inches=\"tight\")\n",
    "\n",
    "plt.figure()\n",
    "handles, labels = fig_ε.axes[1].get_legend_handles_labels()\n",
    "plt.figlegend(handles, labels, loc=\"center\", ncol=3)\n",
    "plt.savefig(joinpath(dirname(@__DIR__), \"figures\", \"p_system_Fourier_solution_legend.pdf\"), bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
