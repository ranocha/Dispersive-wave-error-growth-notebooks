{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Holm-Hone equation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"setup.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Periodic boundary conditions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function relaxation_functional(γ, unew, uold, param)\n",
    "    @unpack D, D2a, D4a, tmp1, tmp2, tmp3 = param\n",
    "    \n",
    "    @. tmp3 = (1-γ)*uold + γ*unew\n",
    "    mul!(tmp2, D2a, tmp3)\n",
    "    mul!(tmp1, D4a, tmp3)\n",
    "    @. tmp1 = tmp3 * (4*tmp3 - 5*tmp2 + tmp1)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "function relaxation_functional(u, param)\n",
    "    @unpack D, D2a, D4a, tmp1, tmp2 = param\n",
    "    \n",
    "    mul!(tmp2, D2a, u)\n",
    "    mul!(tmp1, D4a, u)\n",
    "    @. tmp1 = u * (4*u - 5*tmp2 + tmp1)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_hh_periodic(u, t, integrator)\n",
    "    @unpack D, D2a, D4a, tmp1, tmp2, tmp3, usol = integrator.p\n",
    "    print(\".\")\n",
    "    \n",
    "    mass = integrate(u, D)\n",
    "    \n",
    "    mul!(tmp2, D2a, u)\n",
    "    mul!(tmp3, D4a, u)\n",
    "    @. tmp1 = 4*u - 5*tmp2 + tmp3\n",
    "    linear = integrate(tmp1, D)\n",
    "    \n",
    "    @. tmp1 = u * tmp1\n",
    "    quadratic = integrate(tmp1, D)\n",
    "    \n",
    "    x = grid(D)\n",
    "    tmp1 .= ( usol.(t, x, x[1], -x[1]) .- u ).^2\n",
    "    error_u = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    SVector(mass, linear, quadratic, error_u)\n",
    "end\n",
    "\n",
    "\n",
    "function hh_periodic!(du, u, param, t)\n",
    "    @unpack D, inv4m5D2pD4, D2b, D4b, tmp1, tmp2, tmp3 = param\n",
    "    \n",
    "    # conservative semidiscretization\n",
    "    mul!(tmp1, D2b, u)\n",
    "    mul!(tmp2, D4b, u)\n",
    "    @. tmp1 = -(4 * u - 5 * tmp1 + tmp2)\n",
    "    @. tmp2 = u * tmp1\n",
    "    mul!(tmp3, D, tmp2)\n",
    "    mul!(tmp2, D, u)\n",
    "    @. tmp3 = tmp3 + tmp2 * tmp1\n",
    "    ldiv!(du, inv4m5D2pD4, tmp3)\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "function solve_ode_hh_periodic(usol, D, D2a, D2b, D4a, D4b, tspan, alg, tol, dt, adaptive)\n",
    "    # lu instead of fatorize because of https://github.com/JuliaLang/julia/issues/30084\n",
    "    inv4m5D2pD4 = isa(D2a, AbstractMatrix) ? lu(4*I - 5*D2a + D4a) : 4*I - 5*D2a + D4a\n",
    "    \n",
    "    x = grid(D)\n",
    "    u0 = usol.(tspan[1], x, x[1], -x[1])\n",
    "    tmp1 = similar(u0); tmp2 = similar(tmp1); tmp3 = similar(tmp1)\n",
    "    param = (D=D, D2a=D2a, D2b=D2b, D4a=D4a, D4b=D4b, inv4m5D2pD4=inv4m5D2pD4, \n",
    "             tmp1=tmp1, tmp2=tmp2, tmp3=tmp3, usol=usol)\n",
    "\n",
    "    ode = ODEProblem(hh_periodic!, u0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=100)\n",
    "    saved_values_baseline = SavedValues(eltype(D), SVector{4,eltype(D)})\n",
    "    saving_baseline = SavingCallback(save_func_hh_periodic, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D), SVector{4,eltype(D)})\n",
    "    saving_relaxation = SavingCallback(save_func_hh_periodic, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!, save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    \n",
    "    sol_relaxation = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_relaxation, tstops=saveat)\n",
    "    flush(stdout)\n",
    "    sol_baseline = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_baseline, tstops=saveat)\n",
    "    flush(stdout)\n",
    "\n",
    "    unum_baseline   = sol_baseline[end]\n",
    "    unum_relaxation = sol_relaxation[end]\n",
    "    uana = usol.(tspan[end], x, x[1], -x[1])\n",
    "    @printf(\"Error in u (baseline):   %.3e\\n\", integrate(u->u^2, unum_baseline - uana, D) |> sqrt)\n",
    "    @printf(\"Error in u (relaxation): %.3e\\n\", integrate(u->u^2, unum_relaxation - uana, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in u: %.3e\\n\", \n",
    "        integrate(u->u^2, unum_baseline - unum_relaxation, D) |> sqrt)\n",
    "\n",
    "    sleep(0.1)\n",
    "    fig_u, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, u0, label=L\"u^0\")\n",
    "    plt.plot(x, uana, label=L\"$u^\\mathrm{ana}$\")\n",
    "    plt.plot(x, unum_baseline, label=L\"$u^\\mathrm{num}$ (non-conservative)\")\n",
    "    plt.plot(x, unum_relaxation, label=L\"$u^\\mathrm{num}$ (conservative)\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    mass_baseline        = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_relaxation      = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    linear_baseline      = map(x->x[2], saved_values_baseline.saveval)\n",
    "    linear_relaxation    = map(x->x[2], saved_values_relaxation.saveval)\n",
    "    quadratic_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    quadratic_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_baseline   .- mass_baseline[1], \n",
    "        label=L\"$\\int u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_relaxation .- mass_relaxation[1], \n",
    "        label=L\"$\\int u$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   linear_baseline   .- linear_baseline[1], \n",
    "        label=L\"$\\int (4 - 5 \\partial_x^2 + \\partial_x^4) u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, linear_relaxation .- linear_relaxation[1], \n",
    "        label=L\"$\\int (4 - 5 \\partial_x^2 + \\partial_x^4) u$ (conservative)\", color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.plot(t_baseline,   quadratic_baseline   .- quadratic_baseline[1], \n",
    "        label=L\"$\\int u (4 - 5 \\partial_x^2 + \\partial_x^4) u$ (non-conservative)\", color=\"#E69F00\", linestyle=\":\")\n",
    "    plt.plot(t_relaxation, quadratic_relaxation .- quadratic_relaxation[1], \n",
    "        label=L\"$\\int u (4 - 5 \\partial_x^2 + \\partial_x^4) u$ (conservative)\", color=\"#56B4E9\", linestyle=\":\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_u_baseline   = map(x->x[end], saved_values_baseline.saveval)\n",
    "    error_u_relaxation = map(x->x[end], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error, ax = plt.subplots(1, 1)\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_u_baseline, \n",
    "        label=L\"Error of $u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, error_u_relaxation, \n",
    "        label=L\"Error of $u$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    (; sol_relaxation, sol_baseline, saved_values_baseline, saved_values_relaxation, \n",
    "        fig_u, fig_invariants, fig_error)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# traveling wave solution obtained numerically\n",
    "c, data = open(\"hh_traveling_wave_init_c12_ah01_l40_N65536.txt\", \"r\") do io\n",
    "    line = readline(io)\n",
    "    line = readline(io)\n",
    "    line = readline(io)\n",
    "    c = parse(Float64, line[8:end])\n",
    "    data = readdlm(io, comments=true)\n",
    "    c, data\n",
    "end\n",
    "x = range(data[1,1], data[end,1], length=size(data,1))\n",
    "xmin = x[1]; xmax = x[end]\n",
    "u0 = data[:, 2]\n",
    "u0itp = CubicSplineInterpolation((x,), u0, extrapolation_bc=Periodic())\n",
    "get_c() = c\n",
    "function usol(t, x, xmin, xmax)\n",
    "    c = get_c()\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    u0itp(x_t)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"c = \", get_c())\n",
    "println(\"xmin = \", xmin)\n",
    "println(\"xmax = \", xmax)\n",
    "@show usol(0., xmin, xmin, xmax)\n",
    "@show usol(0., xmax, xmin, xmax)\n",
    "@show N = 2^9\n",
    "@show dt = 0.5 * (xmax - xmin) / (N * get_c())\n",
    "\n",
    "@show tspan = (0.0, (xmax-xmin)/(3*get_c()) + 100*(xmax-xmin)/get_c())\n",
    "flush(stdout)\n",
    "\n",
    "tol = 1.0e-8\n",
    "adaptive = true\n",
    "D = fourier_derivative_operator(xmin, xmax, N)\n",
    "D2a = D2b = D^2; D4a = D4b = D^4\n",
    "\n",
    "results = solve_ode_hh_periodic(usol, D, D2a, D2b, D4a, D4b, tspan, Tsit5(), tol, dt, adaptive);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ax = results.fig_error.axes[1]\n",
    "t = [1.0e2, 6.0e3]\n",
    "ax.plot(t, 1.0e-9 .* t.^2, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{2})\", (1.0e3, 8.0e-3), color=\"gray\")\n",
    "ax.plot(t, 5.0e-10 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e3, 2.0e-7), color=\"gray\")\n",
    "\n",
    "results.fig_error.savefig(joinpath(dirname(@__DIR__), \"figures\", \"hh_error.pdf\"), \n",
    "    bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autoclose": false,
   "autocomplete": true,
   "bibliofile": "biblio.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
