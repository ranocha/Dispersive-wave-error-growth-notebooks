{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using Printf\n",
    "using SparseArrays\n",
    "\n",
    "using UnPack\n",
    "using OrdinaryDiffEq, DiffEqCallbacks\n",
    "using Roots\n",
    "using StaticArrays\n",
    "using SummationByPartsOperators\n",
    "\n",
    "using PyCall, LaTeXStrings; import PyPlot; plt=PyPlot\n",
    "inset_locator = pyimport(\"mpl_toolkits.axes_grid.inset_locator\")\n",
    "\n",
    "cycler = pyimport(\"cycler\").cycler\n",
    "line_cycler   = (cycler(color=[\"#E69F00\", \"#56B4E9\", \"#009E73\", \"#0072B2\", \"#D55E00\", \"#CC79A7\", \"#F0E442\"]) +\n",
    "                 cycler(linestyle=[\"-\", \"--\", \"-.\", \":\", \"-\", \"--\", \"-.\"]))\n",
    "marker_cycler = (cycler(color=[\"#E69F00\", \"#56B4E9\", \"#009E73\", \"#0072B2\", \"#D55E00\", \"#CC79A7\", \"#F0E442\"]) +\n",
    "                 cycler(linestyle=[\"none\", \"none\", \"none\", \"none\", \"none\", \"none\", \"none\"]) +\n",
    "                 cycler(marker=[\"4\", \"2\", \"3\", \"1\", \"+\", \"x\", \".\"]))\n",
    "\n",
    "plt.rc(\"axes\", prop_cycle=line_cycler)\n",
    "plt.rc(\"text\", usetex=true)\n",
    "plt.rc(\"text.latex\", preamble=\"\\\\usepackage{newpxtext}\\\\usepackage{newpxmath}\\\\usepackage{commath}\\\\usepackage{mathtools}\")\n",
    "plt.rc(\"font\", family=\"serif\", size=18.)\n",
    "plt.rc(\"savefig\", dpi=100)\n",
    "plt.rc(\"legend\", loc=\"best\", fontsize=\"medium\", fancybox=true, framealpha=0.5)\n",
    "plt.rc(\"lines\", linewidth=2.5, markersize=10, markeredgewidth=2.5)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function relaxation!(integrator)\n",
    "    told = integrator.tprev\n",
    "    uold = integrator.uprev\n",
    "    tnew = integrator.t\n",
    "    unew = integrator.u\n",
    "\n",
    "    terminate_integration = false\n",
    "    \n",
    "    # General approach using a scalar root finding procedure\n",
    "    γ = one(tnew)\n",
    "    γlo = one(γ) / 2\n",
    "    γhi = 3*one(γ) / 2\n",
    "    energy_old = relaxation_functional(uold, integrator.p)\n",
    "    sign_condition = (relaxation_functional(γlo, unew, uold, integrator.p)-energy_old) * (relaxation_functional(γhi, unew, uold, integrator.p)-energy_old)\n",
    "    if iszero(sign_condition)\n",
    "        # everything is fine\n",
    "        γ = one(tnew)\n",
    "    elseif sign_condition > 0\n",
    "        terminate_integration = true\n",
    "        @warn \"Terminating integration because no solution γ can be found.\"\n",
    "    else\n",
    "        γ = find_zero(g -> relaxation_functional(g, unew, uold, integrator.p)-energy_old, (γlo, γhi), Roots.AlefeldPotraShi())\n",
    "    end\n",
    "    \n",
    "    if γ < eps(typeof(γ))\n",
    "        terminate_integration = true\n",
    "        @warn \"Terminating integration because γ=$γ is too small.\"\n",
    "    end\n",
    "\n",
    "    @. unew = uold + γ * (unew - uold)\n",
    "    DiffEqBase.set_u!(integrator, unew)\n",
    "    if !(tnew ≈ first(integrator.opts.tstops))\n",
    "        tγ = told + γ * (tnew - told)\n",
    "        # doesn't work always, see https://github.com/SciML/OrdinaryDiffEq.jl/issues/1353\n",
    "        # DiffEqBase.set_t!(integrator, tγ)\n",
    "        integrator.t = tγ\n",
    "    end\n",
    "\n",
    "    if terminate_integration\n",
    "        terminate!(integrator)\n",
    "    end\n",
    "\n",
    "    nothing\n",
    "end\n",
    "\n",
    "\n",
    "function quadratic_projection!(integrator)\n",
    "#     uold = integrator.uprev\n",
    "    uold = integrator.sol.prob.u0\n",
    "    unew = integrator.u\n",
    "    @unpack D1 = integrator.sol.prob.p\n",
    "    \n",
    "    factor = sqrt(integrate(u -> u^2, uold, D1) / integrate(u -> u^2, unew, D1))\n",
    "    unew .= factor .* unew\n",
    "    \n",
    "    DiffEqBase.set_u!(integrator, unew)\n",
    "    nothing\n",
    "end\n",
    "\n",
    "\n",
    "function relaxation_functional(γ, unew, uold, param)\n",
    "    @unpack D1, tmp1 = param\n",
    "    \n",
    "    @. tmp1 = (1-γ)*uold + γ*unew\n",
    "    energy = integrate(u -> u^2, tmp1, D1)\n",
    "end\n",
    "\n",
    "function relaxation_functional(u, param)\n",
    "    @unpack D1 = param\n",
    "    \n",
    "    energy = integrate(u -> u^2, u, D1)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_KdV(u, t, integrator)\n",
    "    @unpack D1, tmp1, usol = integrator.p\n",
    "    print(\".\")\n",
    "    \n",
    "    mass = integrate(u, D1)\n",
    "    \n",
    "    energy = integrate(u -> u^2, u, D1)\n",
    "    \n",
    "    x = grid(D1)\n",
    "    tmp1 .= ( usol.(t, x) .- u ).^2\n",
    "    error_u = integrate(tmp1, D1) |> sqrt\n",
    "    \n",
    "    SVector(mass, energy, error_u)\n",
    "end\n",
    "\n",
    "\n",
    "function KdV_nonlinear!(du, u, param, t)\n",
    "    @unpack D1, tmp1, tmp2 = param\n",
    "    \n",
    "    # conservative semidiscretization\n",
    "    mul!(tmp1, D1, u)\n",
    "    @. du = u * tmp1\n",
    "    @. tmp1 = u^2\n",
    "    mul!(tmp2, D1, tmp1)\n",
    "    @. du = -(du + tmp2) / 3\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "\n",
    "function solve_ode_KdV(usol, D1, D3, tspan, alg, tol, dt, adaptive; kwargs...)\n",
    "\n",
    "    x = grid(D1)\n",
    "    u0 = usol.(tspan[1], x)\n",
    "    tmp1 = similar(u0); tmp2 = similar(tmp1)\n",
    "    param = (; D1, D3, tmp1, tmp2, usol)\n",
    "    \n",
    "    linear_part = DiffEqArrayOperator(-D3)\n",
    "    nonlin_part = KdV_nonlinear!\n",
    "    ode = SplitODEProblem(linear_part, nonlin_part, u0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=100)\n",
    "    saved_values_baseline = SavedValues(eltype(D1), SVector{3,eltype(D1)})\n",
    "    saving_baseline = SavingCallback(save_func_KdV, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D1), SVector{3,eltype(D1)})\n",
    "    saving_relaxation = SavingCallback(save_func_KdV, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!, save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    saved_values_projection = SavedValues(eltype(D1), SVector{3,eltype(D1)})\n",
    "    saving_projection = SavingCallback(save_func_KdV, saved_values_projection, saveat=saveat)\n",
    "    projection = DiscreteCallback((u,t,integrator) -> true, quadratic_projection!, save_positions=(false,false))\n",
    "    cb_projection = CallbackSet(projection, saving_projection)\n",
    "    \n",
    "    println(\"Relaxation:\")\n",
    "    @time sol_relaxation = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_relaxation, tstops=saveat, saveat=saveat, maxiters=10^8; kwargs...)\n",
    "    flush(stdout)\n",
    "    println(\"Projection:\")\n",
    "    @time sol_projection = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_projection, tstops=saveat, saveat=saveat, maxiters=10^8; kwargs...)\n",
    "    flush(stdout)\n",
    "    println(\"Baseline:\")\n",
    "    @time sol_baseline = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_baseline, tstops=saveat, saveat=saveat, maxiters=10^8; kwargs...)\n",
    "    println()\n",
    "    flush(stdout)\n",
    "\n",
    "    unum_baseline   = sol_baseline[end]\n",
    "    unum_relaxation = sol_relaxation[end]\n",
    "    unum_projection = sol_projection[end]\n",
    "    uana = usol.(tspan[end], x)\n",
    "    @printf(\"Error in u (baseline):   %.3e\\n\", integrate(u->u^2, unum_baseline - uana, D1) |> sqrt)\n",
    "    @printf(\"Error in u (relaxation): %.3e\\n\", integrate(u->u^2, unum_relaxation - uana, D1) |> sqrt)\n",
    "    @printf(\"Error in u (projection): %.3e\\n\", integrate(u->u^2, unum_projection - uana, D1) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in u: %.3e\\n\", \n",
    "        integrate(u->u^2, unum_baseline - unum_relaxation, D1) |> sqrt)\n",
    "\n",
    "    sleep(0.1)\n",
    "    fig_u, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, unum_baseline, label=\"non-conservative\")\n",
    "    plt.plot(x, unum_relaxation, label=\"relaxation\")\n",
    "    plt.plot(x, unum_projection, label=\"projection\")\n",
    "    plt.plot(x, uana, \":\", color=\"gray\", label=\"analytical\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    t_projection = saved_values_projection.t\n",
    "    mass_baseline      = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_relaxation    = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    mass_projection    = map(x->x[1], saved_values_projection.saveval)\n",
    "    quadratic_baseline   = map(x->x[2], saved_values_baseline.saveval)\n",
    "    quadratic_relaxation = map(x->x[2], saved_values_relaxation.saveval)\n",
    "    quadratic_projection = map(x->x[2], saved_values_projection.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_baseline   .- mass_baseline[1], \n",
    "                label=L\"$\\int u$ (non-conservative)\",\n",
    "                color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_relaxation .- mass_relaxation[1], \n",
    "                label=L\"$\\int u$ (relaxation)\",\n",
    "                color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_projection .- mass_projection[1], \n",
    "                label=L\"$\\int u$ (projection)\",\n",
    "                color=\"#009E73\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   quadratic_baseline   .- quadratic_baseline[1], \n",
    "                label=L\"$\\int u^2$ (non-conservative)\",\n",
    "                color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, quadratic_relaxation .- quadratic_relaxation[1], \n",
    "                label=L\"$\\int u^2$ (relaxation)\",\n",
    "                color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, quadratic_projection .- quadratic_projection[1], \n",
    "                label=L\"$\\int u^2$ (projection)\",\n",
    "                color=\"#009E73\", linestyle=\"--\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_u_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    error_u_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "    error_u_projection = map(x->x[3], saved_values_projection.saveval)\n",
    "\n",
    "    fig_error, ax = plt.subplots(1, 1)\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_u_baseline, label=\"non-conservative\")\n",
    "    plt.plot(t_relaxation, error_u_relaxation, label=\"relaxation\")\n",
    "    plt.plot(t_projection, error_u_projection, label=\"projection\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    fig_legend = plt.figure()\n",
    "    handles, labels = fig_u.axes[1].get_legend_handles_labels()\n",
    "    plt.figlegend(handles, labels, loc=\"center\", ncol=3)\n",
    "        \n",
    "    (;  sol_baseline, sol_relaxation, sol_projection,\n",
    "        saved_values_baseline, saved_values_relaxation, saved_values_projection,\n",
    "        fig_u, fig_invariants, fig_error, fig_legend)\n",
    "end\n",
    "\n",
    "\n",
    "# traveling wave solution\n",
    "get_xmin() = 0.0\n",
    "get_xmax() = 80.0\n",
    "get_c() = 2 / 3\n",
    "function usol(t, x)\n",
    "    xmin = get_xmin()\n",
    "    xmax = get_xmax()\n",
    "    μ = (xmax - xmin) / 2\n",
    "    c = get_c()\n",
    "    A = 3 * c\n",
    "    K = sqrt(1/c - 1)\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin - μ\n",
    "    \n",
    "    A / cosh(sqrt(3*A) / 6 * x_t)^2\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@show get_c()\n",
    "@show xmin = get_xmin()\n",
    "@show xmax = get_xmax()\n",
    "@show usol(0., xmin)\n",
    "@show usol(0., xmax)\n",
    "@show N = 2^8\n",
    "@show dt = 0.1 * (get_xmax() - get_xmin()) / N\n",
    "@show tspan = (0.0, (xmax-xmin)/(3*get_c()) + 30*(xmax-xmin)/get_c())\n",
    "flush(stdout)\n",
    "\n",
    "D1 = periodic_derivative_operator(1, 8, xmin, xmax, N)\n",
    "D3 = periodic_derivative_operator(3, 8, xmin, xmax, N) |> sparse\n",
    "\n",
    "tol = 1.0e-7\n",
    "adaptive = true\n",
    "results = solve_ode_KdV(usol, D1, D3, tspan, KenCarp4(), tol, dt, adaptive);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = plt.subplots(1, 1)\n",
    "x = grid(D1)\n",
    "for idx in round.(Int, range(1, length(results.sol_projection.u), length=6))\n",
    "    plt.plot(x, results.sol_projection.u[idx], label=@sprintf(\"\\$t = %.1f\\$\", results.sol_projection.t[idx]))\n",
    "end\n",
    "plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "plt.xlim(get_xmin(), get_xmax())\n",
    "plt.ylim(-5.0e-3, 5.0e-3)\n",
    "plt.savefig(joinpath(\"..\", \"figures\", \"kdv_projection_zoom.pdf\"), bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = plt.subplots(1, 1)\n",
    "x = grid(D1)\n",
    "plt.plot(x, results.sol_baseline.u[end], label=\"non-conservative\")\n",
    "plt.plot(x, results.sol_relaxation.u[end], label=\"relaxation\")\n",
    "plt.plot(x, results.sol_projection.u[end], label=\"projection\")\n",
    "plt.plot(x, usol.(last(tspan), x), \":\", color=\"gray\", label=\"analytical\")\n",
    "plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "plt.xlim(get_xmin(), get_xmax())\n",
    "plt.savefig(joinpath(\"..\", \"figures\", \"kdv_solution.pdf\"), bbox_inches=\"tight\")\n",
    "\n",
    "plt.figure()\n",
    "handles, labels = fig.axes[1].get_legend_handles_labels()\n",
    "plt.figlegend(handles, labels, loc=\"center\", ncol=4)\n",
    "plt.savefig(joinpath(\"..\", \"figures\", \"kdv_legend.pdf\"), bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = plt.subplots(1, 1)\n",
    "ax.set_xscale(\"log\")\n",
    "ax.set_yscale(\"log\")\n",
    "plt.plot(results.sol_baseline.t,   map(x->x[3], results.saved_values_baseline.saveval), label=\"non-conservative\")\n",
    "plt.plot(results.sol_projection.t, map(x->x[3], results.saved_values_relaxation.saveval), label=\"relaxation\")\n",
    "plt.plot(results.sol_relaxation.t, map(x->x[3], results.saved_values_projection.saveval), label=\"projection\")\n",
    "plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "\n",
    "t = [2.0e2, 4.0e3]\n",
    "ax.plot(t, 2.0e-7 .* t.^2, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{2})\", (1.0e3, 1.0), color=\"gray\")\n",
    "ax.plot(t, 5.0e-6 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e3, 1.5e-2), color=\"gray\")\n",
    "\n",
    "plt.savefig(joinpath(\"..\", \"figures\", \"kdv_error.pdf\"), bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
