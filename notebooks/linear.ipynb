{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Linear dispersive equation with BBM-type dispersive term"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"setup.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Discretization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function relaxation_functional(γ, unew, uold, param)\n",
    "    @unpack D, D2, tmp1, tmp2 = param\n",
    "    \n",
    "    @. tmp2 = (1-γ)*uold + γ*unew\n",
    "    mul!(tmp1, D2, tmp2)\n",
    "    @. tmp1 = tmp2^2 - tmp2 * tmp1\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "function relaxation_functional(u, param)\n",
    "    @unpack D, D2, tmp1 = param\n",
    "    \n",
    "    mul!(tmp1, D2, u)\n",
    "    @. tmp1 = u^2 - u * tmp1\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_linear_periodic(u, t, integrator)\n",
    "    @unpack D, D2, tmp1, usol = integrator.p\n",
    "    print(\".\")\n",
    "    \n",
    "    mass = integrate(u, D)\n",
    "    \n",
    "    mul!(tmp1, D2, u)\n",
    "    @. tmp1 = u^2 - u * tmp1\n",
    "    quadratic = integrate(tmp1, D)\n",
    "    \n",
    "    x = grid(D)\n",
    "    tmp1 .= ( usol.(t, x, x[1], -x[1]) .- u ).^2\n",
    "    error_u = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    SVector(mass, quadratic, error_u)\n",
    "end\n",
    "\n",
    "\n",
    "function linear_periodic!(du, u, param, t)\n",
    "    @unpack D, invImD2, tmp1, tmp2 = param\n",
    "    \n",
    "    # conservative semidiscretization\n",
    "    mul!(tmp1, D, u)\n",
    "    @. tmp1 = -tmp1\n",
    "    ldiv!(du, invImD2, tmp1)\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "function solve_ode_linear_periodic(usol, D, D2, tspan, alg, tol, dt, adaptive)\n",
    "    invImD2 = isa(D2, AbstractMatrix) ? factorize(I - D2) : I - D2\n",
    "    \n",
    "    x = grid(D)\n",
    "    u0 = usol.(tspan[1], x, x[1], -x[1])\n",
    "    tmp1 = similar(u0); tmp2 = similar(tmp1)\n",
    "    param = (D=D, D2=D2, invImD2=invImD2, tmp1=tmp1, tmp2=tmp2, usol=usol)\n",
    "\n",
    "    ode = ODEProblem(linear_periodic!, u0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=100)\n",
    "    saved_values_baseline = SavedValues(eltype(D), SVector{3,eltype(D)})\n",
    "    saving_baseline = SavingCallback(save_func_linear_periodic, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D), SVector{3,eltype(D)})\n",
    "    saving_relaxation = SavingCallback(save_func_linear_periodic, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!, save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    \n",
    "    sol_relaxation = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_relaxation, tstops=saveat, saveat=saveat, maxiters=10^8)\n",
    "    flush(stdout)\n",
    "    sol_baseline = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_baseline, tstops=saveat, saveat=saveat, maxiters=10^8)\n",
    "    flush(stdout)\n",
    "\n",
    "    unum_baseline   = sol_baseline[end]\n",
    "    unum_relaxation = sol_relaxation[end]\n",
    "    uana = usol.(tspan[end], x, x[1], -x[1])\n",
    "    @printf(\"Error in u (baseline):   %.3e\\n\", integrate(u->u^2, unum_baseline - uana, D) |> sqrt)\n",
    "    @printf(\"Error in u (relaxation): %.3e\\n\", integrate(u->u^2, unum_relaxation - uana, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in u: %.3e\\n\", \n",
    "        integrate(u->u^2, unum_baseline - unum_relaxation, D) |> sqrt)\n",
    "\n",
    "    sleep(0.1)\n",
    "    fig_u, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, unum_baseline, label=\"non-conservative\")\n",
    "    plt.plot(x, unum_relaxation, label=\"conservative\")\n",
    "    plt.plot(x, uana, \":\", color=\"gray\", label=\"analytical\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    mass_baseline      = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_relaxation    = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    quadratic_baseline   = map(x->x[2], saved_values_baseline.saveval)\n",
    "    quadratic_relaxation = map(x->x[2], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_baseline   .- mass_baseline[1], \n",
    "                label=L\"$\\int u$ (non-conservative)\",\n",
    "                color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_relaxation .- mass_relaxation[1], \n",
    "                label=L\"$\\int u$ (conservative)\",\n",
    "                color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   quadratic_baseline   .- quadratic_baseline[1], \n",
    "                label=L\"$\\int (u^2 + (\\partial_x u)^2)$ (non-conservative)\",\n",
    "                color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, quadratic_relaxation .- quadratic_relaxation[1], \n",
    "                label=L\"$\\int (u^2 + (\\partial_x u)^2)$ (conservative)\",\n",
    "                color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_u_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    error_u_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error, ax = plt.subplots(1, 1)\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_u_baseline, label=L\"Error of $u$ (non-conservative)\")\n",
    "    plt.plot(t_relaxation, error_u_relaxation, label=L\"Error of $u$ (conservative)\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "#     plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    fig_legend = plt.figure()\n",
    "    handles, labels = fig_u.axes[1].get_legend_handles_labels()\n",
    "    plt.figlegend(handles, labels, loc=\"center\", ncol=3)\n",
    "        \n",
    "    (;sol_baseline, sol_relaxation, saved_values_baseline, saved_values_relaxation, \n",
    "        fig_u, fig_invariants, fig_error, fig_legend)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Error growth of traveling wave solutions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# traveling wave solution\n",
    "xmin = -1.\n",
    "xmax = -xmin\n",
    "get_c() = 1 / (1 + 4*π^2)\n",
    "function usol(t, x, xmin, xmax)\n",
    "    c = get_c()\n",
    "    K = sqrt(1/c - 1)\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    \n",
    "    sin(K * x_t)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"c = \", get_c())\n",
    "@show xmin\n",
    "@show xmax\n",
    "@show usol(0., xmin, xmin, xmax)\n",
    "@show usol(0., xmax, xmin, xmax)\n",
    "@show N = 2^6\n",
    "@show dt = 1.0e-1 * (xmax - xmin) / N\n",
    "flush(stdout)\n",
    "\n",
    "# tspan = (0.0, (xmax-xmin)/(3*get_c()) + 1*(xmax-xmin)/get_c())\n",
    "# tspan = (0.0, (xmax-xmin)/(3*get_c()) + 10*(xmax-xmin)/get_c())\n",
    "tspan = (0.0, (xmax-xmin)/(3*get_c()) + 50_000*(xmax-xmin)/get_c())\n",
    "\n",
    "tol = 1.0e-5\n",
    "adaptive = true\n",
    "D = fourier_derivative_operator(xmin, xmax, N)\n",
    "D2 = D^2\n",
    "\n",
    "results = solve_ode_linear_periodic(usol, D, D2, tspan, Tsit5(), tol, dt, adaptive);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ax = results.fig_error.axes[1]\n",
    "t = [1.0e3, 5.0e6]\n",
    "ax.plot(t, 1.0e-7 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e5, 5.0e-3), color=\"gray\")\n",
    "\n",
    "results.fig_error.savefig(joinpath(dirname(@__DIR__), \"figures\", \"linear_error.pdf\"), \n",
    "    bbox_inches=\"tight\")\n",
    "\n",
    "results.fig_u.savefig(joinpath(dirname(@__DIR__), \"figures\", \"linear_solution_u.pdf\"), bbox_inches=\"tight\")\n",
    "results.fig_legend.savefig(joinpath(dirname(@__DIR__), \"figures\", \"linear_solution_legend.pdf\"), bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = plt.subplots(1, 1)\n",
    "ax.set_xscale(\"log\")\n",
    "ax.set_yscale(\"log\")\n",
    "for line in results.fig_error.axes[1].lines\n",
    "    ax.plot(line.get_xdata(), line.get_ydata())\n",
    "end\n",
    "t = [1.0e3, 5.0e6]\n",
    "ax.plot(t, 1.0e-7 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e5, 5.0e-3), color=\"gray\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autoclose": false,
   "autocomplete": true,
   "bibliofile": "biblio.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
