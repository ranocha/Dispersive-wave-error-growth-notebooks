{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Camassa-Holm equation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"setup.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Discretization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function relaxation_functional(γ, unew, uold, param)\n",
    "    @unpack D, D2a, tmp1, tmp2 = param\n",
    "    \n",
    "    @. tmp2 = (1-γ)*uold + γ*unew\n",
    "    mul!(tmp1, D2a, tmp2)\n",
    "    @. tmp1 = tmp2^2 - tmp2 * tmp1\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "function relaxation_functional(u, param)\n",
    "    @unpack D, D2a, tmp1 = param\n",
    "    \n",
    "    mul!(tmp1, D2a, u)\n",
    "    @. tmp1 = u^2 - u * tmp1\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_ch_periodic(u, t, integrator)\n",
    "    @unpack D, D2a, tmp1, usol = integrator.p\n",
    "    print(\".\")\n",
    "    \n",
    "    mass = integrate(u, D)\n",
    "    \n",
    "    mul!(tmp1, D2a, u)\n",
    "    @. tmp1 = u^2 - u * tmp1\n",
    "    quadratic = integrate(tmp1, D)\n",
    "    \n",
    "    x = grid(D)\n",
    "    tmp1 .= ( usol.(t, x, x[1], -x[1]) .- u ).^2\n",
    "    error_u = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    SVector(mass, quadratic, error_u)\n",
    "end\n",
    "\n",
    "\n",
    "function ch_periodic!(du, u, param, t)\n",
    "    @unpack D, invImD2a, D2b, tmp1, tmp2 = param\n",
    "    \n",
    "    # conservative semidiscretization\n",
    "    mul!(du, D, u)\n",
    "    @. tmp1 = -u * du\n",
    "    mul!(tmp2, D2b, tmp1)\n",
    "    @. tmp1 = tmp1 - 0.5 * tmp2\n",
    "    mul!(du, D2b, u)\n",
    "    @. tmp2 = 0.5 * u * du - u^2\n",
    "    mul!(du, D, tmp2)\n",
    "    @. tmp1 = tmp1 + du\n",
    "    ldiv!(du, invImD2a, tmp1)\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "function solve_ode_ch_periodic(usol, D, D2a, D2b, tspan, alg, tol, dt, adaptive)\n",
    "    invImD2a = isa(D2a, AbstractMatrix) ? factorize(I - D2a) : I - D2a\n",
    "    \n",
    "    x = grid(D)\n",
    "    u0 = usol.(tspan[1], x, x[1], -x[1])\n",
    "    tmp1 = similar(u0); tmp2 = similar(tmp1)\n",
    "    param = (D=D, D2a=D2a, D2b=D2b, invImD2a=invImD2a, tmp1=tmp1, tmp2=tmp2, usol=usol)\n",
    "\n",
    "    ode = ODEProblem(ch_periodic!, u0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=100)\n",
    "    saved_values_baseline = SavedValues(eltype(D), SVector{3,eltype(D)})\n",
    "    saving_baseline = SavingCallback(save_func_ch_periodic, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D), SVector{3,eltype(D)})\n",
    "    saving_relaxation = SavingCallback(save_func_ch_periodic, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!, save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    \n",
    "    sol_relaxation = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_relaxation, tstops=saveat, saveat=saveat, maxiters=10^8)\n",
    "    flush(stdout)\n",
    "    sol_baseline = solve(ode, alg, abstol=tol, reltol=tol, dt=dt, adaptive=adaptive, save_everystep=false,\n",
    "        callback=cb_baseline, tstops=saveat, saveat=saveat, maxiters=10^8)\n",
    "    flush(stdout)\n",
    "\n",
    "    unum_baseline   = sol_baseline[end]\n",
    "    unum_relaxation = sol_relaxation[end]\n",
    "    uana = usol.(tspan[end], x, x[1], -x[1])\n",
    "    println()\n",
    "    @printf(\"Error in u (baseline):   %.3e\\n\", integrate(u->u^2, unum_baseline - uana, D) |> sqrt)\n",
    "    @printf(\"Error in u (relaxation): %.3e\\n\", integrate(u->u^2, unum_relaxation - uana, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in u: %.3e\\n\", \n",
    "        integrate(u->u^2, unum_baseline - unum_relaxation, D) |> sqrt)\n",
    "\n",
    "    sleep(0.1)\n",
    "    fig_u, ax = plt.subplots(1, 1)\n",
    "    plt.plot(x, u0, label=L\"u^0\")\n",
    "    plt.plot(x, uana, label=L\"$u^\\mathrm{ana}$\")\n",
    "    plt.plot(x, unum_baseline, label=L\"$u^\\mathrm{num}$ (non-conservative)\")\n",
    "    plt.plot(x, unum_relaxation, label=L\"$u^\\mathrm{num}$ (conservative)\")\n",
    "    plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    mass_baseline      = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_relaxation    = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    quadratic_baseline   = map(x->x[2], saved_values_baseline.saveval)\n",
    "    quadratic_relaxation = map(x->x[2], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_baseline   .- mass_baseline[1], \n",
    "        label=L\"$\\int u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_relaxation .- mass_relaxation[1], \n",
    "        label=L\"$\\int u$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   quadratic_baseline   .- quadratic_baseline[1], \n",
    "        label=L\"$\\int (u^2 + (\\partial_x u)^2)$ (non-conservative)\", color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, quadratic_relaxation .- quadratic_relaxation[1], \n",
    "        label=L\"$\\int (u^2 + (\\partial_x u)^2)$ (conservative)\", color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_u_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    error_u_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error, ax = plt.subplots(1, 1)\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_u_baseline, \n",
    "        label=L\"Error of $u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, error_u_relaxation, \n",
    "        label=L\"Error of $u$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "        \n",
    "    (; sol_relaxation, sol_baseline, saved_values_baseline, saved_values_relaxation, \n",
    "        fig_u, fig_invariants, fig_error)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Error growth of smooth solitary wave solutions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# traveling wave solution obtained numerically\n",
    "c, data = open(\"ch_traveling_wave_init_c12_ah01_l40_N65536.txt\", \"r\") do io\n",
    "    line = readline(io)\n",
    "    line = readline(io)\n",
    "    line = readline(io)\n",
    "    c = parse(Float64, line[8:end])\n",
    "    data = readdlm(io, comments=true)\n",
    "    c, data\n",
    "end\n",
    "x = range(data[1,1], data[end,1], length=size(data,1))\n",
    "xmin = x[1]; xmax = x[end]\n",
    "u0 = data[:, 2]\n",
    "u0itp = CubicSplineInterpolation((x,), u0, extrapolation_bc=Periodic())\n",
    "get_c() = c\n",
    "function usol(t, x, xmin, xmax)\n",
    "    c = get_c()\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    u0itp(x_t)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"c = \", get_c())\n",
    "println(\"xmin = \", xmin)\n",
    "println(\"xmax = \", xmax)\n",
    "@show usol(0., xmin, xmin, xmax)\n",
    "@show usol(0., xmax, xmin, xmax)\n",
    "@show N = 2^9\n",
    "@show dt = 0.5 * (xmax - xmin) / (N * get_c())\n",
    "\n",
    "@show tspan = (0.0, (xmax-xmin)/(3*get_c()) + 200*(xmax-xmin)/get_c())\n",
    "flush(stdout)\n",
    "\n",
    "tol = 1.0e-7\n",
    "adaptive = true\n",
    "D = fourier_derivative_operator(xmin, xmax, N)\n",
    "D2a = D^2; D2b = D^2\n",
    "\n",
    "results = solve_ode_ch_periodic(usol, D, D2a, D2b, tspan, Tsit5(), tol, dt, adaptive);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ax = results.fig_error.axes[1]\n",
    "t = [1.0e3, 1.0e4]\n",
    "ax.plot(t, 4.0e-8 .* t.^2, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{2})\", (1.0e3, 2.0e-1), color=\"gray\")\n",
    "ax.plot(t, 4.0e-8 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e3, 1.5e-4), color=\"gray\")\n",
    "\n",
    "results.fig_error.savefig(joinpath(dirname(@__DIR__), \"figures\", \"ch_error.pdf\"), \n",
    "    bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Error growth of a peakon solution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "xmin = -35.0\n",
    "xmax = -xmin\n",
    "get_c() = 1.2\n",
    "function usol(t, x, xmin, xmax)\n",
    "    c = get_c()\n",
    "    x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "    c * exp(-abs(x_t))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"c = \", get_c())\n",
    "println(\"xmin = \", xmin)\n",
    "println(\"xmax = \", xmax)\n",
    "@show usol(0., xmin, xmin, xmax)\n",
    "@show usol(0., xmax, xmin, xmax)\n",
    "@show N = 2^13\n",
    "@show dt = 0.5 * (xmax - xmin) / (N * get_c())\n",
    "\n",
    "@show tspan = (0.0, (xmax-xmin)/(3*get_c()) + 4*(xmax-xmin)/get_c())\n",
    "flush(stdout)\n",
    "\n",
    "tol = 1.0e-7\n",
    "adaptive = true\n",
    "D = periodic_derivative_operator(1, 8, xmin, xmax, N+1)\n",
    "D2a = D2b = periodic_derivative_operator(2, 8, xmin, xmax, N+1)\n",
    "\n",
    "results = solve_ode_ch_periodic(usol, D, D2a, D2b, tspan, Tsit5(), tol, dt, adaptive);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "t_baseline   = results.saved_values_baseline.t\n",
    "t_relaxation = results.saved_values_relaxation.t\n",
    "error_u_baseline   = map(x->x[3], results.saved_values_baseline.saveval)\n",
    "error_u_relaxation = map(x->x[3], results.saved_values_relaxation.saveval)\n",
    "\n",
    "fig_error, ax = plt.subplots(1, 1)\n",
    "ax.set_xscale(\"log\")\n",
    "ax.set_yscale(\"log\")\n",
    "plt.plot(t_baseline,   error_u_baseline, \n",
    "    label=L\"Error of $u$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "plt.plot(t_relaxation, error_u_relaxation, \n",
    "    label=L\"Error of $u$ (conservative)\", color=\"#56B4E9\", linestyle=\"--\")\n",
    "plt.xlabel(L\"t\"); plt.ylabel(L\"Error of $u$\")\n",
    "# plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "\n",
    "t = [1.0e1, 1.0e2]\n",
    "ax.plot(t, 7.0e-5 .* t.^2, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{2})\", (2.0e1, 1.0e-1), color=\"gray\")\n",
    "t = [1.0e1, 2.0e2]\n",
    "ax.plot(t, 7.0e-4 .* t.^1, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{1})\", (1.0e2, 1.5e-1), color=\"gray\")\n",
    "\n",
    "fig_error.savefig(joinpath(dirname(@__DIR__), \"figures\", \"ch_peakon_error.pdf\"), \n",
    "    bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = grid(D)\n",
    "unum_baseline   = results.sol_baseline[end]\n",
    "unum_relaxation = results.sol_relaxation[end]\n",
    "uana = usol.(tspan[end], x, x[1], -x[1])\n",
    "\n",
    "fig_u, ax = plt.subplots(1, 1)\n",
    "plt.plot(x, unum_baseline, label=L\"$u^\\mathrm{num}$ (non-conservative)\")\n",
    "plt.plot(x, unum_relaxation, label=L\"$u^\\mathrm{num}$ (conservative)\")\n",
    "plt.plot(x, uana, label=L\"$u^\\mathrm{ana}$\", \":\", color=\"gray\")\n",
    "plt.xlabel(L\"x\"); plt.ylabel(L\"u\")\n",
    "# plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "fig_u.savefig(joinpath(dirname(@__DIR__), \"figures\", \"ch_peakon_solution.pdf\"), \n",
    "    bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autoclose": false,
   "autocomplete": true,
   "bibliofile": "biblio.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
