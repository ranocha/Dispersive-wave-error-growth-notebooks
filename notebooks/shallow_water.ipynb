{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Shallow water equations with variable bathymetry and periodic BCs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"setup.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Discretization in 2D"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function local_energy(h, hvx, hvy, g, b)\n",
    "    0.5 * ((hvx^2 + hvy^2) / h + g * h^2) + g * h * b\n",
    "end\n",
    "\n",
    "function relaxation_functional(γ, u_new, u_old, param)\n",
    "    @unpack D, g, b, tmp1 = param\n",
    "    h_new   = u_new.x[1]\n",
    "    hvx_new = u_new.x[2]\n",
    "    hvy_new = u_new.x[3]\n",
    "    h_old   = u_old.x[1]\n",
    "    hvx_old = u_old.x[2]\n",
    "    hvy_old = u_old.x[3]\n",
    "    \n",
    "    @. tmp1 = local_energy((1-γ)*h_old + γ*h_new, \n",
    "                           (1-γ)*hvx_old + γ*hvx_new, \n",
    "                           (1-γ)*hvy_old + γ*hvy_new, g, b)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "function relaxation_functional(u, param)\n",
    "    @unpack D, g, b, tmp1 = param\n",
    "    h   = u.x[1]\n",
    "    hvx = u.x[2]\n",
    "    hvy = u.x[3]\n",
    "    \n",
    "    @. tmp1 = local_energy(h, hvx, hvy, g, b)\n",
    "    energy = integrate(tmp1, D)\n",
    "end\n",
    "\n",
    "\n",
    "function save_func_periodic(u, t, integrator)\n",
    "    @unpack D, tmp1, h_sol, hvx_sol, hvy_sol = integrator.p\n",
    "    h   = u.x[1]\n",
    "    hvx = u.x[2]\n",
    "    hvy = u.x[3]\n",
    "    print(\".\")\n",
    "    \n",
    "    mass_h   = integrate(h, D)\n",
    "    mass_hvx = integrate(hvx, D)\n",
    "    mass_hvy = integrate(hvy, D)\n",
    "    energy = relaxation_functional(u, integrator.p)\n",
    "    \n",
    "    xy = grid(D)\n",
    "    @. tmp1 = (h - h_sol(t, xy))^2\n",
    "    error_h = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    @. tmp1 = (hvx - hvx_sol(t, xy))^2\n",
    "    error_hvx = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    @. tmp1 = (hvy - hvy_sol(t, xy))^2\n",
    "    error_hvy = integrate(tmp1, D) |> sqrt\n",
    "    \n",
    "    min_h,   max_h   = extrema(h)\n",
    "    min_hvx, max_hvx = extrema(hvx)\n",
    "    min_hvy, max_hvy = extrema(hvy)\n",
    "    \n",
    "    SVector(mass_h, mass_hvx, mass_hvy, energy, error_h, error_hvx, error_hvy,\n",
    "            min_h, max_h, min_hvx, max_hvx, min_hvy, max_hvy)\n",
    "end\n",
    "\n",
    "\n",
    "function shallow_water_periodic!(du, u, param, t)\n",
    "    @unpack D, g, b, tmp1, tmp2, vx, vy = param\n",
    "    h   = u.x[1]\n",
    "    hvx = u.x[2]\n",
    "    hvy = u.x[3]\n",
    "    dh   = du.x[1]\n",
    "    dhvx = du.x[2]\n",
    "    dhvy = du.x[3]\n",
    "    \n",
    "    @. vx = hvx / h\n",
    "    @. vy = hvy / h\n",
    "    \n",
    "    mul!(tmp1, D, hvx, Val(:x))\n",
    "    mul!(tmp2, D, hvy, Val(:y))\n",
    "    @. dh = -tmp1 - tmp2\n",
    "    @. dhvx = -0.5 * vx * tmp1\n",
    "    @. dhvy = -0.5 * vy * tmp2\n",
    "    \n",
    "    mul!(tmp1, D, hvx, Val(:y))\n",
    "    mul!(tmp2, D, hvy, Val(:x))\n",
    "    @. dhvx -= 0.5 * vy * tmp1\n",
    "    @. dhvy -= 0.5 * vx * tmp2\n",
    "    \n",
    "    @. tmp1 = (h + b)\n",
    "    mul!(tmp2, D, tmp1, Val(:x))\n",
    "    @. dhvx -= g * h * tmp2\n",
    "    mul!(tmp2, D, tmp1, Val(:y))\n",
    "    @. dhvy -= g * h * tmp2\n",
    "    \n",
    "    @. tmp1 = hvx * vx\n",
    "    mul!(tmp2, D, tmp1, Val(:x))\n",
    "    @. dhvx -= 0.5 * tmp2\n",
    "    @. tmp1 = hvy * vy\n",
    "    mul!(tmp2, D, tmp1, Val(:y))\n",
    "    @. dhvy -= 0.5 * tmp2\n",
    "    \n",
    "    mul!(tmp1, D, vx, Val(:x))\n",
    "    @. dhvx -= 0.5 * hvx * tmp1\n",
    "    @. dhvy -= 0.5 * hvy * tmp1\n",
    "    mul!(tmp1, D, vy, Val(:y))\n",
    "    @. dhvx -= 0.5 * hvx * tmp1\n",
    "    @. dhvy -= 0.5 * hvy * tmp1\n",
    "    \n",
    "    @. tmp1 = hvx * vy\n",
    "    mul!(tmp2, D, tmp1, Val(:y))\n",
    "    @. dhvx -= 0.5 * tmp2\n",
    "    mul!(tmp2, D, tmp1, Val(:x))\n",
    "    @. dhvy -= 0.5 * tmp2\n",
    "    \n",
    "    nothing\n",
    "end\n",
    "\n",
    "function solve_ode_shallow_water_periodic(h_sol, hvx_sol, hvy_sol, g, b_sol, \n",
    "                                          D, tspan, alg, abstol, reltol, dt, adaptive)\n",
    "    xy = grid(D)\n",
    "    h0   = h_sol.(  tspan[1], xy)\n",
    "    hvx0 = hvx_sol.(tspan[1], xy)\n",
    "    hvy0 = hvy_sol.(tspan[1], xy)\n",
    "    b    = b_sol.(  tspan[1], xy)\n",
    "    u0 = ArrayPartition(h0, hvx0, hvy0)\n",
    "    tmp1 = similar(h0); tmp2 = similar(h0);\n",
    "    vx   = similar(h0); vy   = similar(h0);\n",
    "    param = (;D, g, b, tmp1, tmp2, vx, vy, h_sol, hvx_sol, hvy_sol, b_sol)\n",
    "\n",
    "    ode = ODEProblem(shallow_water_periodic!, u0, tspan, param)\n",
    "    \n",
    "    saveat = range(tspan..., length=100)\n",
    "    saved_values_baseline = SavedValues(eltype(D), SVector{13,eltype(D)})\n",
    "    saving_baseline = SavingCallback(save_func_periodic, saved_values_baseline, saveat=saveat)\n",
    "    saved_values_relaxation = SavedValues(eltype(D), SVector{13,eltype(D)})\n",
    "    saving_relaxation = SavingCallback(save_func_periodic, saved_values_relaxation, saveat=saveat)\n",
    "    relaxation = DiscreteCallback((u,t,integrator) -> true, relaxation!, \n",
    "        save_positions=(false,false))\n",
    "    cb_baseline = CallbackSet(saving_baseline)\n",
    "    cb_relaxation = CallbackSet(relaxation, saving_relaxation)\n",
    "    \n",
    "    saveat_full = [tspan[1], 0.95*tspan[1]+0.05*tspan[end], \n",
    "                             0.75*tspan[1]+0.25*tspan[end],\n",
    "                             0.50*tspan[1]+0.50*tspan[end], \n",
    "                             0.25*tspan[1]+0.75*tspan[end], tspan[end]]\n",
    "    \n",
    "    @time sol_relaxation = solve(ode, alg, abstol=abstol, reltol=reltol, dt=dt, adaptive=adaptive, \n",
    "        save_everystep=false, callback=cb_relaxation, tstops=saveat, saveat=saveat_full)\n",
    "    flush(stdout)\n",
    "    @time sol_baseline = solve(ode, alg, abstol=abstol, reltol=reltol, dt=dt, adaptive=adaptive, \n",
    "        save_everystep=false, callback=cb_baseline, tstops=saveat, saveat=saveat_full)\n",
    "    flush(stdout)\n",
    "\n",
    "    h_num_baseline     = sol_baseline[end].x[1]\n",
    "    hvx_num_baseline   = sol_baseline[end].x[2]\n",
    "    hvy_num_baseline   = sol_baseline[end].x[3]\n",
    "    h_num_relaxation   = sol_relaxation[end].x[1]\n",
    "    hvx_num_relaxation = sol_relaxation[end].x[2]\n",
    "    hvy_num_relaxation = sol_relaxation[end].x[3]\n",
    "#     h_ana   = h_sol.(tspan[end], x)\n",
    "#     hvx_ana = hvx_sol.(tspan[end], x)\n",
    "#     hvy_ana = hvy_sol.(tspan[end], x)\n",
    "#     @printf(\"Error in h   (baseline):   %.3e\\n\", integrate(u->u^2, h_num_baseline    - h_ana, D) |> sqrt)\n",
    "#     @printf(\"Error in h   (relaxation): %.3e\\n\", integrate(u->u^2, h_num_relaxation  - h_ana, D) |> sqrt)\n",
    "#     @printf(\"Error in hvx (baseline):   %.3e\\n\", integrate(u->u^2, hvx_num_baseline   - hvx_ana, D) |> sqrt)\n",
    "#     @printf(\"Error in hvx (relaxation): %.3e\\n\", integrate(u->u^2, hvx_num_relaxation - hvx_ana, D) |> sqrt)\n",
    "#     @printf(\"Error in hvy (baseline):   %.3e\\n\", integrate(u->u^2, hvy_num_baseline   - hvy_ana, D) |> sqrt)\n",
    "#     @printf(\"Error in hvy (relaxation): %.3e\\n\", integrate(u->u^2, hvy_num_relaxation - hvy_ana, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in h:   %.3e\\n\", \n",
    "        integrate(u->u^2, h_num_baseline  - h_num_relaxation, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in hvx: %.3e\\n\", \n",
    "        integrate(u->u^2, hvx_num_baseline - hvx_num_relaxation, D) |> sqrt)\n",
    "    @printf(\"Difference of baseline and relaxation in hvy: %.3e\\n\", \n",
    "        integrate(u->u^2, hvy_num_baseline - hvy_num_relaxation, D) |> sqrt)\n",
    "\n",
    "#     sleep(0.1)\n",
    "#     fig_h, ax = plt.subplots(1, 1)\n",
    "#     plt.plot(x, h0 + b, label=L\"h^0 + b\")\n",
    "# #     plt.plot(x, h_ana + b, label=L\"$h^\\mathrm{ana} + b$\")\n",
    "#     plt.plot(x, h_num_baseline + b,   label=L\"$h^\\mathrm{num} + b$ (baseline)\")\n",
    "#     plt.plot(x, h_num_relaxation + b, label=L\"$h^\\mathrm{num} + b$ (relaxation)\")\n",
    "#     plt.xlabel(L\"x\"); plt.ylabel(L\"h + b\")\n",
    "#     plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "#     fig_hv, ax = plt.subplots(1, 1)\n",
    "#     plt.plot(x, hv0, label=L\"hv^0\")\n",
    "# #     plt.plot(x, hv_ana, label=L\"$u^\\mathrm{ana}$\")\n",
    "#     plt.plot(x, hv_num_baseline,   label=L\"$hv^\\mathrm{num}$ (baseline)\")\n",
    "#     plt.plot(x, hv_num_relaxation, label=L\"$hv^\\mathrm{num}$ (relaxation)\")\n",
    "#     plt.xlabel(L\"x\"); plt.ylabel(L\"hv\")\n",
    "#     plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5));\n",
    "\n",
    "    t_baseline = saved_values_baseline.t\n",
    "    t_relaxation = saved_values_relaxation.t\n",
    "    mass_h_baseline     = map(x->x[1], saved_values_baseline.saveval)\n",
    "    mass_h_relaxation   = map(x->x[1], saved_values_relaxation.saveval)\n",
    "    mass_hvx_baseline   = map(x->x[2], saved_values_baseline.saveval)\n",
    "    mass_hvx_relaxation = map(x->x[2], saved_values_relaxation.saveval)\n",
    "    mass_hvy_baseline   = map(x->x[3], saved_values_baseline.saveval)\n",
    "    mass_hvy_relaxation = map(x->x[3], saved_values_relaxation.saveval)\n",
    "    energy_baseline     = map(x->x[4], saved_values_baseline.saveval)\n",
    "    energy_relaxation   = map(x->x[4], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_invariants, ax = plt.subplots(1, 1)\n",
    "    ax.set_yscale(\"symlog\", linthreshy=1.0e-14)\n",
    "    plt.plot(t_baseline,   mass_h_baseline   .- mass_h_baseline[1],   \n",
    "        label=L\"$\\int h$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, mass_h_relaxation .- mass_h_relaxation[1], \n",
    "        label=L\"$\\int h$ (conservative)\", color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   mass_hvx_baseline   .- mass_hvx_baseline[1],   \n",
    "        label=L\"$\\int hv_x$ (non-conservative)\", color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, mass_hvx_relaxation .- mass_hvx_relaxation[1], \n",
    "        label=L\"$\\int hv_x$ (conservative)\", color=\"#56B4E9\", linestyle=\"--\")\n",
    "#     plt.plot(t_baseline,   mass_hvy_baseline   .- mass_hvy_baseline[1],   \n",
    "#         label=L\"$\\int hv_y$ (non-conservative)\", color=\"#E69F00\", linestyle=\"-.\")\n",
    "#     plt.plot(t_relaxation, mass_hvy_relaxation .- mass_hvy_relaxation[1], \n",
    "#         label=L\"$\\int hv_y$ (conservative)\", color=\"#56B4E9\", linestyle=\"-.\")\n",
    "    plt.plot(t_baseline,   energy_baseline   .- energy_baseline[1],   \n",
    "        label=\"Energy (non-conservative)\", color=\"#E69F00\", linestyle=\":\")\n",
    "    plt.plot(t_relaxation, energy_relaxation .- energy_relaxation[1], \n",
    "        label=\"Energy (conservative)\", color=\"#56B4E9\", linestyle=\":\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Change of Invariants\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "    \n",
    "    error_h_baseline     = map(x->x[5], saved_values_baseline.saveval)\n",
    "    error_h_relaxation   = map(x->x[5], saved_values_relaxation.saveval)\n",
    "    error_hvx_baseline   = map(x->x[6], saved_values_baseline.saveval)\n",
    "    error_hvx_relaxation = map(x->x[6], saved_values_relaxation.saveval)\n",
    "    error_hvy_baseline   = map(x->x[7], saved_values_baseline.saveval)\n",
    "    error_hvy_relaxation = map(x->x[7], saved_values_relaxation.saveval)\n",
    "\n",
    "    fig_error, ax = plt.subplots(1, 1)\n",
    "    ax.set_xscale(\"log\")\n",
    "    ax.set_yscale(\"log\")\n",
    "    plt.plot(t_baseline,   error_h_baseline,   label=L\"Error of $h$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"-\")\n",
    "    plt.plot(t_relaxation, error_h_relaxation, label=L\"Error of $h$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"-\")\n",
    "    plt.plot(t_baseline,   error_hvx_baseline,   label=L\"Error of $h v_x$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"--\")\n",
    "    plt.plot(t_relaxation, error_hvx_relaxation, label=L\"Error of $h v_x$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"--\")\n",
    "    plt.plot(t_baseline,   error_hvy_baseline,   label=L\"Error of $h v_y$ (non-conservative)\",\n",
    "        color=\"#E69F00\", linestyle=\"-.\")\n",
    "    plt.plot(t_relaxation, error_hvy_relaxation, label=L\"Error of $h v_y$ (conservative)\",\n",
    "        color=\"#56B4E9\", linestyle=\"-.\")\n",
    "    plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "    plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "        \n",
    "    (; sol_relaxation, sol_baseline, fig_invariants, fig_error, saved_values_baseline, saved_values_relaxation)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "# using a diffracton generated numerically\n",
    "function mean_to_center(q::AbstractVector)\n",
    "    nghost = 2\n",
    "    qBCs = similar(q, length(q) + 2*nghost)\n",
    "    qBCs .= zero(eltype(q))\n",
    "    qBCs[1:nghost] = q[end:-1:end-nghost+1]\n",
    "    qBCs[nghost+1:end-nghost] = q\n",
    "    qBCs[end-nghost+1:end] = q[1:nghost]\n",
    "    res = @views @. (9*qBCs[1:end-4] - 116*qBCs[2:end-3] + 2134*qBCs[3:end-2] - 116*qBCs[4:end-1] + 9*qBCs[5:end]) / 1920.0\n",
    "end\n",
    "function mean_to_center(q::AbstractMatrix)\n",
    "    res = copy(q)\n",
    "    for i in 1:size(q, 1)\n",
    "        @views res[i, :] .= mean_to_center(res[i, :])\n",
    "    end\n",
    "    for j in 1:size(q, 2)\n",
    "        @views res[:, j] .= mean_to_center(res[:, j])\n",
    "    end\n",
    "    res\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "let\n",
    "    data_filename = joinpath(dirname(@__DIR__), \"data\", \"shallow_water\", \"refn2_small_domain.h5\")\n",
    "    global get_c() = 2.3043\n",
    "    global get_xmin() = 0.0\n",
    "    global get_xmax() = 20.0\n",
    "    global get_ymin() = -0.5\n",
    "    global get_ymax() = 0.5\n",
    "    \n",
    "    x_center, y_center, h_center, hvx_center, hvy_center, hpb_center, b_center = h5open(data_filename, \"r\") do io\n",
    "        # x, z are the coordinates of the cell centers\n",
    "        x   = read(io, \"x\")\n",
    "        y   = read(io, \"y\")\n",
    "        # the other values are mean cell mean values\n",
    "        h   = read(io, \"h\")\n",
    "        hvx = read(io, \"hu\")\n",
    "        hvy = read(io, \"hv\")\n",
    "        hpb = read(io, \"eta\")\n",
    "        b   = hpb - h\n",
    "\n",
    "        x, y, mean_to_center(h'), mean_to_center(hvx'), mean_to_center(hvy'), mean_to_center(hpb'), mean_to_center(b')\n",
    "    end\n",
    "\n",
    "    dx = (x_center[end] - x_center[1]) / (length(x_center) - 1)\n",
    "    xmin = x_center[1]; xmax = x_center[end]\n",
    "    x = range(xmin, xmax, length=length(x_center))\n",
    "\n",
    "    dy = (y_center[end] - y_center[1]) / (length(y_center) - 1)\n",
    "    ymin = y_center[1]; ymax = y_center[end]\n",
    "    y = range(ymin, ymax, length=length(y_center))\n",
    "    \n",
    "    h0itp   = CubicSplineInterpolation((x,y), mean_to_center(h_center),   extrapolation_bc=Periodic())\n",
    "    hpb0itp = CubicSplineInterpolation((x,y), mean_to_center(hpb_center), extrapolation_bc=Periodic())\n",
    "    b0itp   = CubicSplineInterpolation((x,y), mean_to_center(b_center),   extrapolation_bc=Periodic())\n",
    "    hvx0itp = CubicSplineInterpolation((x,y), mean_to_center(hvx_center), extrapolation_bc=Periodic())\n",
    "    hvy0itp = CubicSplineInterpolation((x,y), mean_to_center(hvy_center), extrapolation_bc=Periodic())\n",
    "\n",
    "    global get_g() = 9.8\n",
    "    global function b_sol(t, xy)\n",
    "        x, y = xy\n",
    "        b0itp(x, y)::Float64\n",
    "    end\n",
    "    global function h_sol(t, xy)\n",
    "        x, y = xy\n",
    "        c = get_c()\n",
    "        xmin = get_xmin()\n",
    "        xmax = get_xmax()\n",
    "        x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "\n",
    "        h0itp(x_t, y)::Float64\n",
    "    end\n",
    "    global function hvx_sol(t, xy)\n",
    "        x, y = xy\n",
    "        c = get_c()\n",
    "        xmin = get_xmin()\n",
    "        xmax = get_xmax()\n",
    "        x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "\n",
    "        hvx0itp(x_t, y)::Float64\n",
    "    end\n",
    "    global function hvy_sol(t, xy)\n",
    "        x, y = xy\n",
    "        c = get_c()\n",
    "        xmin = get_xmin()\n",
    "        xmax = get_xmax()\n",
    "        x_t = mod(x - c*t - xmin, xmax - xmin) + xmin\n",
    "\n",
    "        hvy0itp(x_t, y)::Float64\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@show Nx = 2^10\n",
    "@show Ny = 2^6\n",
    "dt = 1.0 * max((get_xmax() - get_xmin()) / Nx, (get_ymax() - get_ymin()) / Ny) / get_c()\n",
    "@show dt\n",
    "\n",
    "tspan = (0.0, 15*(get_xmax() - get_xmin())/get_c())\n",
    "@show tspan\n",
    "flush(stdout)\n",
    "\n",
    "abstol = reltol = 1.0e-6\n",
    "adaptive = true\n",
    "D = fourier_derivative_operator(get_xmin(), get_xmax(), Nx,\n",
    "                                get_ymin(), get_ymax(), Ny)\n",
    "\n",
    "@time results = solve_ode_shallow_water_periodic(\n",
    "    h_sol, hvx_sol, hvy_sol, get_g(), b_sol, \n",
    "    D, tspan, Tsit5(), abstol, reltol, dt, adaptive);\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "xy, h_initial  = evaluate_coefficients(results.sol_baseline.u[1].x[1], D)\n",
    "_, hvx_initial = evaluate_coefficients(results.sol_baseline.u[1].x[2], D)\n",
    "_, hvy_initial = evaluate_coefficients(results.sol_baseline.u[1].x[3], D)\n",
    "x = first.(xy[:, 1]); y = last.(xy[1, :])\n",
    "b = results.sol_baseline.prob.p.b\n",
    "\n",
    "idx = length(results.sol_baseline)\n",
    "_, h_baseline   = evaluate_coefficients(results.sol_baseline.u[idx].x[1], D)\n",
    "_, hvx_baseline = evaluate_coefficients(results.sol_baseline.u[idx].x[2], D)\n",
    "_, hvy_baseline = evaluate_coefficients(results.sol_baseline.u[idx].x[3], D)\n",
    "\n",
    "_, h_relaxation   = evaluate_coefficients(results.sol_relaxation.u[idx].x[1], D)\n",
    "_, hvx_relaxation = evaluate_coefficients(results.sol_relaxation.u[idx].x[2], D)\n",
    "_, hvy_relaxation = evaluate_coefficients(results.sol_relaxation.u[idx].x[3], D)\n",
    "\n",
    "bson(joinpath(dirname(@__DIR__), \"data\", \"shallow_water_Fourier.bson\"), \n",
    "    Dict(:saved_values_baseline=>results.saved_values_baseline, \n",
    "         :saved_values_relaxation=>results.saved_values_relaxation,\n",
    "         :x=>x, :y=>y, :b=>b,\n",
    "         :h_initial=>h_initial, :hvx_initial=>hvx_initial, :hvy_initial=>hvy_initial,\n",
    "         :h_baseline=>h_baseline, :hvx_baseline=>hvx_baseline, :hvy_baseline=>hvy_baseline,\n",
    "         :h_relaxation=>h_relaxation, :hvx_relaxation=>hvx_relaxation, :hvy_relaxation=>hvy_relaxation))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# load saved data\n",
    "saved_results = BSON.load(joinpath(dirname(@__DIR__), \"data\", \"shallow_water_Fourier.bson\"))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "t_baseline   = saved_results[:saved_values_baseline].t\n",
    "t_relaxation = saved_results[:saved_values_relaxation].t\n",
    "error_h_baseline     = map(x->x[5], saved_results[:saved_values_baseline].saveval)\n",
    "error_h_relaxation   = map(x->x[5], saved_results[:saved_values_relaxation].saveval)\n",
    "error_hvx_baseline   = map(x->x[6], saved_results[:saved_values_baseline].saveval)\n",
    "error_hvx_relaxation = map(x->x[6], saved_results[:saved_values_relaxation].saveval)\n",
    "error_hvy_baseline   = map(x->x[7], saved_results[:saved_values_baseline].saveval)\n",
    "error_hvy_relaxation = map(x->x[7], saved_results[:saved_values_relaxation].saveval)\n",
    "\n",
    "fig_error, ax = plt.subplots(1, 1)\n",
    "ax.set_xscale(\"log\")\n",
    "ax.set_yscale(\"log\")\n",
    "plt.plot(t_baseline,   error_h_baseline,   label=L\"Error of $h$ (non-conservative)\",\n",
    "    color=\"#E69F00\", linestyle=\"-\")\n",
    "plt.plot(t_relaxation, error_h_relaxation, label=L\"Error of $h$ (conservative)\",\n",
    "    color=\"#56B4E9\", linestyle=\"-\")\n",
    "plt.plot(t_baseline,   error_hvx_baseline,   label=L\"Error of $h v_x$ (non-conservative)\",\n",
    "    color=\"#E69F00\", linestyle=\"--\")\n",
    "plt.plot(t_relaxation, error_hvx_relaxation, label=L\"Error of $h v_x$ (conservative)\",\n",
    "    color=\"#56B4E9\", linestyle=\"--\")\n",
    "plt.plot(t_baseline,   error_hvy_baseline,   label=L\"Error of $h v_y$ (non-conservative)\",\n",
    "    color=\"#E69F00\", linestyle=\"-.\")\n",
    "plt.plot(t_relaxation, error_hvy_relaxation, label=L\"Error of $h v_y$ (conservative)\",\n",
    "    color=\"#56B4E9\", linestyle=\"-.\")\n",
    "plt.xlabel(L\"t\"); plt.ylabel(\"Error\")\n",
    "plt.legend(loc=\"center left\", bbox_to_anchor=(1.0, 0.5))\n",
    "\n",
    "t = [1.0e1, 1.3e2]\n",
    "ax.plot(t, 2.0e-4 .* t .^ 0.7, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{0.7})\", (4.0e1, 5.0e-3), color=\"gray\")\n",
    "ax.plot(t, 3.0e-4 .* t .^ 0.3, \":\", color=\"gray\")\n",
    "ax.annotate(L\"\\mathcal{O}(t^{0.3})\", (4.0e1, 7.0e-4), color=\"gray\")\n",
    "\n",
    "savefig(\n",
    "    joinpath(dirname(@__DIR__), \"figures\", \"shallow_water_Fourier_error.pdf\"),\n",
    "    bbox_inches=\"tight\")\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_single_heatmap!(ax, x, _y, _data, label; kwargs...)\n",
    "    y = copy(_y); push!(y, 2*y[end]-y[end-1])\n",
    "    data = similar(_data, (size(_data,1), size(_data,2)+1))\n",
    "    data[:, 1:end-1] = _data; data[:, end] = _data[:, 1]\n",
    "    pcm = ax.pcolormesh(x, y, data', linewidth=0, rasterized=true; kwargs...)\n",
    "    pcm.set_edgecolor(\"face\")\n",
    "    ax.set_ylabel(L\"y\")\n",
    "    cbar = plt.colorbar(pcm, label=label, ax=ax)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function plot_heatmaps(x, y, b, h, hvx, hvy, filename=\"\")\n",
    "    fig, ax = plt.subplots(3, 1, figsize=(14,3), sharex=true)\n",
    "    plot_single_heatmap!(ax[1], x, y, h+b, L\"h + b\"; vmin=0.74, vmax=0.81)\n",
    "    plot_single_heatmap!(ax[2], x, y, hvx, L\"h v_x\"; vmin=-0.15, vmax=0.15)\n",
    "    plot_single_heatmap!(ax[3], x, y, hvy, L\"h v_y\"; vmin=-0.015, vmax=0.015)\n",
    "    ax[3].set_xlabel(L\"x\")\n",
    "    plt.subplots_adjust(hspace=0.4)\n",
    "    if !isempty(filename)\n",
    "        fig.savefig(filename, bbox_inches=\"tight\")\n",
    "    end\n",
    "    return fig\n",
    "end\n",
    "\n",
    "plot_heatmaps(saved_results[:x], saved_results[:y], saved_results[:b],\n",
    "              saved_results[:h_initial], saved_results[:hvx_initial], saved_results[:hvy_initial],\n",
    "              joinpath(dirname(@__DIR__), \"figures\", \"shallow_water_Fourier_initial.pdf\")\n",
    ")\n",
    "plot_heatmaps(saved_results[:x], saved_results[:y], saved_results[:b],\n",
    "              saved_results[:h_baseline], saved_results[:hvx_baseline], saved_results[:hvy_baseline],\n",
    "              joinpath(dirname(@__DIR__), \"figures\", \"shallow_water_Fourier_baseline.pdf\")\n",
    ")\n",
    "plot_heatmaps(saved_results[:x], saved_results[:y], saved_results[:b],\n",
    "              saved_results[:h_relaxation], saved_results[:hvx_relaxation], saved_results[:hvy_relaxation],\n",
    "              joinpath(dirname(@__DIR__), \"figures\", \"shallow_water_Fourier_relaxation.pdf\")\n",
    ");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autoclose": false,
   "autocomplete": true,
   "bibliofile": "references.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
